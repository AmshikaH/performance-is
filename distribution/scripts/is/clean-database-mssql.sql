-- Create procedure for dropping foreign keys of specified table.
CREATE OR ALTER PROCEDURE DROP_FOREIGN_KEYS
	(@tableName NVARCHAR(50))
AS
	BEGIN
		DECLARE @sqlCmd NVARCHAR(MAX) = N'';
		SELECT @sqlCmd += 'ALTER TABLE [' +  OBJECT_SCHEMA_NAME(parent_object_id) +
		'].[' + OBJECT_NAME(parent_object_id) + '] DROP CONSTRAINT [' + name + '];'
		FROM sys.foreign_keys
		WHERE referenced_object_id = object_id(@tableName);
		EXEC sp_executesql @sqlCmd;
	END;
GO

USE IDENTITY_DB;

-- Drop foreign keys.
EXEC DROP_FOREIGN_KEYS N'IDN_OAUTH2_AUTHORIZATION_CODE';
EXEC DROP_FOREIGN_KEYS N'IDN_OAUTH2_ACCESS_TOKEN';

-- Add tables that need to be truncated.
TRUNCATE TABLE IDN_AUTH_SESSION_STORE;
TRUNCATE TABLE IDN_AUTH_TEMP_SESSION_STORE;
TRUNCATE TABLE IDN_OAUTH2_AUTHORIZATION_CODE;
TRUNCATE TABLE IDN_OAUTH2_ACCESS_TOKEN_SCOPE;
TRUNCATE TABLE IDN_OAUTH2_ACCESS_TOKEN;

-- Recreate foreign keys.
ALTER TABLE dbo.IDN_OIDC_REQ_OBJECT_REFERENCE
	ADD FOREIGN KEY (CODE_ID) REFERENCES IDN_OAUTH2_AUTHORIZATION_CODE(CODE_ID);
ALTER TABLE dbo.IDN_OAUTH2_AUTHZ_CODE_SCOPE
	ADD FOREIGN KEY (CODE_ID) REFERENCES IDN_OAUTH2_AUTHORIZATION_CODE(CODE_ID) ON DELETE CASCADE;

ALTER TABLE dbo.IDN_OAUTH2_TOKEN_BINDING
	ADD FOREIGN KEY (TOKEN_ID) REFERENCES IDN_OAUTH2_ACCESS_TOKEN(TOKEN_ID) ON DELETE CASCADE;
ALTER TABLE dbo.IDN_OAUTH2_ACCESS_TOKEN_SCOPE
	ADD FOREIGN KEY (TOKEN_ID) REFERENCES IDN_OAUTH2_ACCESS_TOKEN(TOKEN_ID) ON DELETE CASCADE;
ALTER TABLE dbo.IDN_OIDC_REQ_OBJECT_REFERENCE
	ADD FOREIGN KEY (TOKEN_ID) REFERENCES IDN_OAUTH2_ACCESS_TOKEN(TOKEN_ID);
